---
layout: post
title: sed地獄へようこそ――「第27回シェル芸勉強会　大阪サテライト」の記録
permalink: /2017-02-15-01/
---

はじめに
--------

2月11日(土)に開催された「[第27回シェル芸勉強会　大阪サテライト](https://atnd.org/events/85597)」に参加しました。

今季最強の寒波による大雪が予想されるということで、一時は大阪サテライトの開催が危ぶまれましたが、当日の天気は荒れることもなく無事に開催されることになりました。

今回も、朝9時45分から夜18時までひたすらsh行に打ち込む一日でした。

……sed地獄へようこそ。

### メイン会場および他サテライト会場 ###

* [jus共催 第9回初心者満足度ナンバーワン（当社調べ・調べてないけど）シェル勉強会/第27回sedこわいシェル芸勉強会](https://usptomo.doorkeeper.jp/events/56744)
* [福岡サテライト会場：第27回シェル芸勉強会](https://atnd.org/events/85463)

### 各会場の様子 ###

* [jus共催 第27回sedこわいシェル芸勉強会 - Togetterまとめ](https://togetter.com/li/1080314)
* [「第27回シェル芸勉強会　大阪サテライト」レポート - くんすとの備忘録](http://www.kunst1080.net/entry/2017/02/13/235721)
* [第27回シェル芸勉強会へ遠隔参加 - 日々之迷歩](http://papiro.hatenablog.jp/entry/2017/02/12/162647)

### 問題ならびに模範解答 ###

* [【問題のみ】第27回sedこわいシェル芸勉強会 - 上田ブログ](https://blog.ueda.asia/?p=9309)
* [【問題と解答】第27回sedこわいシェル芸勉強会 - 上田ブログ](https://blog.ueda.asia/?p=9283)

### ライブストリーム(YouTube) ###

* [午前の部#1: 黒い画面と戯れよう](https://youtu.be/oeNFq5t_frc)
* [午前の部#2: シェル芸入門 日常会話編](https://youtu.be/cDt2Olr0x84)
* [午後の部#1: 第27回シェル芸勉強会](https://youtu.be/BXM4pmvxrq4)
* [午後の部#2: 東京本会場 LT大会](https://youtu.be/JBax4En3EeM)

------------------------------------------------------------------------------

午前の部: シェルに関する勉強会
------------------------------

### 黒い画面と戯れよう ###

講師: 鳥海秀一さん(USP友の会)

* 「黒い画面怖い」から、どのようにCLIを攻略するか
    * 遊び心を持っていろいろ試す
    * キーボードと黒い画面の2つに分けて考える
* 黒い画面を攻略
    * 文字はゲシュタルトであり、フォント等を変えることで文字が持つ情報以上の何かを伝えることができる
    * bashから見れば単なるファイル
    * エスケープシーケンスで文字表示以外の制御ができる
* エスケープシーケンスに関連するコマンド
    * `clear`
    * `tput`
    * `stty`
* エスケープシーケンスの応用例
    * エスケープシーケンスを名前に持つファイルやディレクトリの作成
    * 第25回勉強会のQ8を解く(サインカーブの描画)

エスケープシーケンスを使ってターミナル画面でいろいろ試してみるという内容で、具体的には`tput`コマンドで`terminfo`データベースを操作することによってターミナル画面を制御しています。

ちなみに、応用例での

    $ touch $(clear)
    $ ls -1
    ?[3;J?[H?[2J

という謎ファイル作成は、いたずらにピッタリだと思います。

あと、サインカーブの描画はただただスゴい!!

### シェル芸入門 日常会話編 ###

講師: [石井久治](https://twitter.com/hisaharu)さん(USP友の会)

* シェル芸とは
    * `curl -s https://blog.ueda.asia/?page_id=1434 | sed 's/<[^<]\+>//g' | sed '/^シェル芸の定義/,+1!d;s/\x0b//g'`
* シェルとは
    * ユーザとカーネル間のインターフェイスとなるソフトウェア
* シェル芸の思想とUNIX哲学
* シェルとコマンドについて
    * 標準入出力
    * リダイレクト
    * パイプ
    * フィルタ
* シェル(bash)で使える要素
    * 組込みコマンド
    * コマンドの複合
    * 制御構造
    * 変数
    * 展開
        * パラメータ展開
        * (...略...)
        * ブレース展開(実演: 表の描画)
        * プロセス置換
    * 外部コマンド
        * (...略...)
        * `cut`
        * `tac`
        * `tee`
        * (...以下略...)
* 参考書籍

シェル芸を基礎から学んで身につけるという初心者向けの内容ですが、中盤からのブレース展開を使った表の描画はまさに「芸」といってよいでしょう。

……しかし、シェル芸の(奥|闇)は深い。

------------------------------------------------------------------------------

休憩
----

午後からのsed地獄に備えるべく、`man sed`で`sed`のマニュアルを読みながらの昼食となりました。

……これは果たして、休憩なのだろうか?

------------------------------------------------------------------------------

午後の部: シェル芸勉強会
------------------------

* 解答にある`sed`のコード片に対して可読性が高いとはお世辞にも言えないものの、参考資料として「[sed, a stream editor](http://web.sfc.wide.ad.jp/~sagawa/gnujdoc/sed-4.1.2/)」をあらかじめ読んでおけば少しは分かりやすくなるかと思います。
* いずれの問題も、Debian 8「jessie」の32ビットPC版で解答しています。

### A1: 偶数番目の文字だけ大文字にする ###

    $ echo abcdefghijklmn | sed -r 's/(.)(.)/\1\u\2/g'
    aBcDeFgHiJkLmN

`sed`の`s`コマンドで置換をする際に利用できる、GNU拡張の

> '\u'<br />
> 　　　　Turn the next character to uppercase,

という特別なエスケープシーケンスを使い、該当する部分を大文字にしています。

奇数番目の文字を大文字にする場合は、

    $ echo abcdefghijklmn | sed -r 's/(.)(.)/\u\1\2/g'
    AbCdEfGhIjKlMn

のようになります。

ちなみに、正規表現を書くときに

> -r, --regexp-extended<br />
> 　　　　スクリプトで拡張正規表現を使用する

というオプションを使うと、バックスラッシュ(**\\**)の数を減らすことができて便利です。

### A2: sedだけでFizzBuzz ###

    $ seq 1 100 | sed '3~3s/.*/Fizz/;5~5s/.*/Buzz/;15~15s/.*/FizzBuzz/'
    1
    2
    Fizz
    4
    Buzz
    Fizz
    7
    8
    Fizz
    Buzz
    11
    Fizz
    13
    14
    FizzBuzz
    (...以下略...)

GNU拡張にある、次のアドレス指定方法

> first~step<br />
> 　　　　first 行からはじまる step 行おきの行にマッチする。<br />
> 　　　　(...以下略...)

でFizzBuzzを行っています。

### A3: 3行目を7行目の下に移動 ###

    $ seq 1 10 | sed -n '3!p;3h;7x;7p'
    1
    2
    4
    5
    6
    7
    3
    8
    9
    10

`sed`の部分の手順は、

1. 3行目以外を表示
2. 3行目の内容をホールドスペースにコピー
3. 7行目でホールドスペースとパターンスペースの内容を交換
4. 7行目でパターンスペース(3行目の内容)を出力

となっています。

なお、ホールドスペースを使った`sed`のコードを書く場合、

> -n, --quiet, --silent<br />
> 　　　　パターンスペースの自動出力を抑制する

というオプションを使うことで、想定外の出力を可能な限り抑えることができます。

### A4: コードの位置を入れ替える ###

    $ cat aho.cc | sed -rz 's/(int main[^}]+})(\n\n)(void aho[^}]+})/\3\2\1/'
    #include <iostream>
    using namespace std;

    void aho(void)
    {
            cout << "aho" << endl;
    }

    int main(int argc, char const* argv[])
    {
            aho();
            return 0;
    }

GNU拡張のオプション

> -z, --null-data<br />
> 　　　　NUL 文字で行を分割する

を使うと、正規表現の中で改行文字(**\n**)を扱うことができますので、これを利用してコードブロックを置換しています。

パターンスペース・ホールドスペースの扱いが苦手でも、工夫次第で何とかなるものです。

### A5: 奇数行と偶数行を入れ替える ###

    $ seq 1 10 | sed -rz 's/\n/ /g;s/([^ ]+) ([^ ]+)/\2 \1/g;s/ /\n/g'
    2
    1
    4
    3
    6
    5
    8
    7
    10
    9

**A4**とほぼ同じ手法で解いています。

なお、次のシェル芸のように、

    $ seq 1 10 | sed -rz 's/([^\n]+)\n([^\n]+)/\2\n\1/g'
    2
    1
    4
    3
    6
    5
    8
    7
    10
    9

改行文字(**\n**)をそのまま正規表現で扱ったほうが、コードゴルフ的には良かったかと思います。

### A6: 「1」で図形を出力――その1 ###

    $ echo 1 | sed -r 'p;:_;s/(.)$/\1\1/p;t_;' | sed '10q'
    1
    11
    111
    1111
    11111
    111111
    1111111
    11111111
    111111111
    1111111111

1つ目の`sed`の部分では、手順として

1. パターンスペースを出力
2. 末尾の文字を1ヶ重複して出力
3. **1.**(ラベル「`_`」)に戻る

となっていますが、このままだと無限に出力し続けるので、2つ目の`sed`の部分で出力を止めています。

また、より短い別解として、

    $ echo 1 | sed -nr ':_;/^.{11}$/b;p;s/.$/&&/;t_'
    1
    11
    111
    1111
    11111
    111111
    1111111
    11111111
    111111111
    1111111111

を紹介しておきます。手順としては、

1. パターンスペースの文字数を確認し、10ヶを超えれば終了するという条件を設定
2. パターンスペースを出力
3. パターンスペースの末尾の文字を1ヶ重複させる
4. **1.**(ラベル「`_`」)に戻る

というふうになっています。

### A7: 縛りsed――複数回のファイルのコピー ###

`a`というファイルを作成した後、このファイルを`a1`〜`a10`まで連番でコピーするわけですが、

* 縛り1: 使うコマンドは`seq`、`cp`、`sed`だけ
* 縛り2: ワンライナー中で数字を使わない

という2つの縛りに従って問題を解く必要があります。

ちなみに、縛り1と縛り2は「AND」の関係ではなく、それぞれの条件に基づいて解答するとのことでしたが、

    $ ls
    $ sed -n 'w a' /etc/passwd; sed '=' a | sed '/^[a-z]/d' | sed -n '/^.$/,/^..$/!d;s/.*/cp a a&/e'
    $ ls
    a  a1  a10  a2  a3  a4  a5  a6  a7  a8  a9

のように両方の条件を同時に満たした解答ができてしまいましたので、これを説明します。

さて、肝心の手順ですが、

1. ほとんどのシステムでは`/etc/passwd`の行数が10行以上あることを利用し、`w`コマンドで`/etc/passwd`から`a`に書き込む
2. `=`コマンドで行数を出力
3. 英文字で始まる行を削除し、行数の部分だけ残す
4. 数字1ヶだけの行と最初に数字2ヶになる行以外を削除した後、`e`フラグ付きの`s`コマンドで`cp`用文字列を生成して実行

という、強引きわまりないものになっています。

実用性はほとんどないと思いますが、よい頭の体操になりました。

### A8: 「1」で図形を出力――その2 ###

*__訂正__: [当日の解答](https://twitter.com/mikkun_jp/status/830305716762091520)に誤りがありましたので、次の通り訂正します。*

    $ echo 1 | sed -nr ':A;p;/^.{5}$/bB;s/.$/&&/;tA;:B;p;/^.$/b;s/.$//;tB'
    1
    11
    111
    1111
    11111
    11111
    1111
    111
    11
    1

`sed`の部分の処理を、ラベル「`A`」で示されるループと、ラベル「`B`」で示されるループの2つに分けて説明すると次のようになります。

* ラベル「`A`」:
    1. パターンスペースを出力
    2. パターンスペースの文字数が5ヶであれば、ラベル「`B`」に移る
    3. パターンスペースの末尾の文字を1ヶ重複させる
    4. **1.**(ラベル「`A`」)に戻る
* ラベル「`B`」:
    1. パターンスペースを出力
    2. パターンスペースの文字数が1ヶであれば、`sed`の処理を終了
    3. パターンスペースの末尾の文字を1ヶ消す
    4. **1.**(ラベル「`B`」)に戻る

`sed`というものが、実はスクリプト言語であることがよく分かる問題でした。

------------------------------------------------------------------------------

LT大会
------

### Q7の解答の説明 ###

発表者: 日柳 光久([@mikkun_jp](https://twitter.com/mikkun_jp))

* (...内容は**A7**での説明と同じため、省略...)

### [sedで知る矢印キーのキーコード](https://speakerdeck.com/motooka/seddezhi-ru-shi-yin-kifalse-kikodo) ###

発表者: T.Motooka([@t_motooka](https://twitter.com/t_motooka))さん

* 矢印キーが何のコードを送っているか?
* `sed -n l`で確認できる
* コナミコマンドを送れると役に立つ?

午前の部で学んだ`touch $(clear)`と並んで、`sed -n l`もターミナル画面を使ったいたずらに有用な気がします。

### [我は放つ、危険のシェル芸！！](https://dl.dropboxusercontent.com/u/54939588/slides/I-Will-Release-Dangerous-Shell-Arts.html) ###

発表者: nmrmsys([@nmrmsys](https://twitter.com/nmrmsys))さん

* [Star Wars™ Force Band™ by Sphero](http://www.sphero.com/starwars/forceband)について
    * 時計型コントロールデバイス
    * [IFTTT](https://ja.wikipedia.org/wiki/IFTTT)と連携できる
* 危険シェル芸の放ち方
    1. [Quick Web Hooker - Secure tunnels to local Python CGI Server](https://github.com/nmrmsys/qwh)をRaspberry Piに仕込む
    2. Force Band™から[ngrok](https://ngrok.com/)経由でRaspberry Piに危険シェル芸を実行するリクエストを送信

「IoTの次は何か?」という話も世の中にはあるようですが、「KoT(Kiken-shellgei on Things)」が来たとしても決しておかしくないでしょう。

30億のデバイスで走る危険シェル芸の世界は、すぐそこなのかも知れません。

------------------------------------------------------------------------------

おわりに
--------

エクシェル芸の[前回](http://mikkun.github.io/2016-12-31-01/)が「地獄の一丁目」だとすれば、`sed`縛りの今回は「地獄の二丁目」といったところでしょうか。

それはともかく、今まで良く分からなかった`sed`の各種コマンドや、ホールドスペース・パターンスペースの使い方を学ぶことができた有意義な一日でした。

最後に、

* 大阪サテライトの主催をされている、くんすと([@kunst1080](https://twitter.com/kunst1080))さん、T.Motooka([@t\_motooka](https://twitter.com/t_motooka))さん
* 大阪サテライト会場を提供してくださっている、[フェンリル株式会社 大阪本社](http://www.fenrir-inc.com/)様
* 午前の部の講師を務めてくださった、鳥海秀一さん、石井久治([@hisaharu](https://twitter.com/hisaharu))さん
* Ryuichi Ueda([@ryuichiueda](https://twitter.com/ryuichiueda))さんをはじめとする、メイン会場スタッフの皆さん

に改めてお礼を申し上げます。ありがとうございました。

……そういえば、Vimシェル芸という何やら不穏な文字列が……。
